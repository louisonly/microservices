--根据service、profile、label查找配置信息
CREATE OR REPLACE 
function myqueryconfig(v_service in varchar, v_profile in varchar, v_label in varchar)
  return tabletype_config_result PIPELINED 
  as
    v rowtype_config_result;
begin
  for thisrow in (SELECT "key", "value" from t_config where "application"=v_service and "profile"=v_profile and "label"=v_label) loop
    v := rowtype_config_result(thisrow."key", thisrow."value");
    pipe row (v);
  end loop;
	return;
end;


CREATE OR REPLACE 
FUNCTION queryfinal (v_service IN VARCHAR, v_rdcid IN VARCHAR, v_customlabel IN VARCHAR)
RETURN tabletype_config_result PIPELINED 
AS
v rowtype_config_result;
v_label VARCHAR(50);
v_profile VARCHAR(50);
BEGIN
	--根据profile、label准确查找相关配置信息
	v_profile := GETENV();
	v_label := getlabel(v_service,v_rdcid,v_customlabel);
	FOR thisrow IN (
	WITH
		Q1 AS (SELECT "key","value" FROM TABLE(MYQUERYCONFIG(v_service,v_profile,v_label))), --精准查找
		Q2 AS (SELECT "key","value" FROM TABLE(MYQUERYCONFIG(v_service,'-',v_label))),  --形如 - male形式的查找
		Q3 AS (SELECT "key","value" FROM TABLE(MYQUERYCONFIG(v_service,v_profile,'-'))), --形如 male - 形式的查找
		Q4 AS (SELECT "key","value" FROM TABLE(MYQUERYCONFIG(v_service,'-','-')))    --形如 - - 形式的查找
		SELECT "key","value" FROM Q1
		UNION
		SELECT "key","value" FROM Q2 t2 WHERE t2."key" NOT IN (SELECT "key" FROM Q1)
		UNION
		SELECT "key","value" FROM Q3 t3 WHERE t3."key" NOT IN (SELECT "key" FROM Q1 UNION SELECT "key" FROM Q2)
		UNION
		SELECT "key","value" FROM Q4 t4 WHERE t4."key" NOT IN (SELECT "key" FROM Q1 UNION SELECT "key" FROM Q2 UNION SELECT "key" FROM Q3)
		) LOOP
		v := rowtype_config_result(thisrow."key", thisrow."value");
		pipe row (v);
  end loop;
  return;
END;
